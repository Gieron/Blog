<!DOCTYPE html>
<html>
<head>
    <meta charset="utf-8" />
    <title>F# for real: a troposphere model | Pseudorandom Knowledge</title>
    <meta name="viewport" content="width=device-width, initial-scale=1">
    <link rel="stylesheet" href="/styles/highlight.css" />
    <link rel="stylesheet" href="/styles/common.css" />
    <script src="/scripts/highlight.js"></script>
    <script src="/scripts/common.js"></script>
</head>
<body>
    <header>
        <a href="/" rel="home">
            <span>Pseudorandom Knowledge</span>
        </a>
    </header>

    <article itemscope itemtype="http://schema.org/BlogPosting">
        <header>
            <h1 itemprop="headline name">F# for real: a troposphere model</h1>
            <time class="published" itemprop="datePublished">2012-05-14</time>
            <time class="modified" itemprop="dateModified">2017-09-13</time>
        </header>

        <content itemprop="articleBody text">

            <p>
                <a href="https://msdn.microsoft.com/visualfsharpdocs/conceptual/visual-fsharp">F#</a> is a functional language for .NET with some imperative elements. It has full support in Visual Studio 10 and later (except for the Express editions). F# can be a useful complement to C# but you have to keep them in separate projects.
            </p>

            <p>
                Instead of making <a href="http://www.tryfsharp.org/">a tutorial</a> we are going to look at a real world example where F# can help us. This is mainly thanks to a feature called <a href="https://blogs.msdn.microsoft.com/andrewkennedy/2008/08/29/units-of-measure-in-f-part-one-introducing-units/">Units of Measure</a> which lets us introduce measurement units into the type system.
            </p>

            <p>
                The example I have written is an implementation of a tropospheric error model for GPS as described in the <a href="http://standards.globalspec.com/std/1014192/rtca-do-229">system specifications for SBAS</a> (an augmentation system for GPS). <em><a href="http://ltu.diva-portal.org/smash/get/diva2:1027218/FULLTEXT01.pdf">Section 6.3 on page 32 in this PDF</a> has more information.</em>
            </p>

            <pre><code class="fsharp">namespace GpsErrorModels
open System
 
// Units and equivalences
[&lt;Measure>] type degree
<span class="highlight-line">[&lt;Measure>] type kelvin
[&lt;Measure>] type kilogram
[&lt;Measure>] type meter
[&lt;Measure>] type second
[&lt;Measure>] type joule = kilogram meter^2 / second^2
[&lt;Measure>] type millibar = kilogram / meter / second^2</span>
 
// Tropospheric model (defined in RTCA/DO-229C) for a specific receiver
// position (latitude and height) and time (date)
type TroposphereModel(latitude:float&lt;degree>, height:float&lt;meter>,
                      time:DateTime) =
 
    let Interpolate_ζ(latitude:float&lt;degree>, values:float&lt;'u> list):float&lt;'u> =
        let interpolate(k, k0, k1, v0, v1):float&lt;'u> =
            // Interpolate (equations A-4 and A-5)
            v0 + (v1 - v0) * (k - k0) / (k1 - k0);
        let latitudes = [15.0&lt;degree>; 30.0&lt;degree>; 45.0&lt;degree>;
                         60.0&lt;degree>; 75.0&lt;degree>;]
        // Interpolation according to table A-2
        if latitude &lt; latitudes.[0] then values.[0]
        elif latitude &lt; latitudes.[1] then
            interpolate(latitude, latitudes.[0], latitudes.[1],
                        values.[0], values.[1])
        elif latitude &lt; latitudes.[2] then
            interpolate(latitude, latitudes.[1], latitudes.[2],
                        values.[1], values.[2])
        elif latitude &lt; latitudes.[3] then
            interpolate(latitude, latitudes.[2], latitudes.[3],
                        values.[2], values.[3])
        else values.[4];
 
    let Get_ζ(latitude:float&lt;degree>, time:DateTime, averages:float&lt;'u> list,
              variations:float&lt;'u> list):float&lt;'u> =
        let ζ0 = Interpolate_ζ(latitude, averages)
        let Δζ = Interpolate_ζ(latitude, variations)
        let D = (float)time.DayOfYear
        let Dmin = if latitude > 0.0&lt;degree> then 28.0 else 211.0
        // Compute meterological parameter (equation A-3)
        ζ0 - Δζ * Math.Cos(2.0 * Math.PI * (D - Dmin) / 365.25);
 
    // Pressure values from table A-2
    let Get_P(latitude:float&lt;degree>, time:DateTime) =
        let averages = [1013.25&lt;millibar>; 1017.25&lt;millibar>; 1015.25&lt;millibar>;
                        1011.25&lt;millibar>; 1013.00&lt;millibar>;]
        let variations = [ 0.00&lt;millibar>; -3.75&lt;millibar>; -2.25&lt;millibar>;
                          -1.75&lt;millibar>; -0.50&lt;millibar>;]
        Get_ζ(latitude, time, averages, variations);
 
    // Temperature values from table A-2
    let Get_T(latitude:float&lt;degree>, time:DateTime) =
        let averages = [299.65&lt;kelvin>; 294.15&lt;kelvin>; 283.25&lt;kelvin>;
                        272.15&lt;kelvin>; 263.65&lt;kelvin>;]
        let variations = [ 0.00&lt;kelvin>; 7.00&lt;kelvin>; 11.00&lt;kelvin>;
                          15.00&lt;kelvin>; 14.50&lt;kelvin>;]
        Get_ζ(latitude, time, averages, variations);
 
    // Water vapor pressure values from table A-2
    let Get_e(latitude:float&lt;degree>, time:DateTime) =
        let averages = [26.31&lt;millibar>; 21.79&lt;millibar>; 11.66&lt;millibar>;
                         6.78&lt;millibar>; 4.11&lt;millibar>;]
        let variations = [0.00&lt;millibar>; 8.85&lt;millibar>; 7.24&lt;millibar>;
                          5.36&lt;millibar>; 3.39&lt;millibar>;]
        Get_ζ(latitude, time, averages, variations);
 
    // Temperature lapse rates from table A-2
    let Get_β(latitude:float&lt;degree>, time:DateTime) =
        let averages = [6.30E-3&lt;kelvin/meter>; 6.05E-3&lt;kelvin/meter>;
                        5.58E-3&lt;kelvin/meter>; 5.39E-3&lt;kelvin/meter>;
                        4.53E-3&lt;kelvin/meter>;]
        let variations = [0.00E-3&lt;kelvin/meter>; 0.25E-3&lt;kelvin/meter>;
                          0.32E-3&lt;kelvin/meter>; 0.81E-3&lt;kelvin/meter>;
                          0.62E-3&lt;kelvin/meter>;]
        Get_ζ(latitude, time, averages, variations);
 
    // Water vapor "lapse rates" from table A-2
    let Get_λ(latitude:float&lt;degree>, time:DateTime) =
        let averages = [2.77&lt;1>; 3.15&lt;1>; 2.57&lt;1>; 1.81&lt;1>; 1.55&lt;1>;]
        let variations = [0.00&lt;1>; 0.33&lt;1>; 0.46&lt;1>; 0.74&lt;1>; 0.30&lt;1>;]
        Get_ζ(latitude, time, averages, variations);
 
    let ZenithDelay:float&lt;meter> =
        // Meteorological values from table A-2
        let P:float&lt;millibar> = Get_P(latitude, time)
<span class="highlight-line">        let T:float&lt;kelvin> = Get_T(latitude, time)
        let e:float&lt;millibar> = Get_e(latitude, time)
        let β:float&lt;kelvin/meter> = Get_β(latitude, time)
        let λ:float&lt;1> = Get_λ(latitude, time)</span>
        // Constants defined on page A-9 and A-10
        let k1 = 77.604&lt;kelvin/millibar>
<span class="highlight-line">        let k2 = 382000.0&lt;kelvin^2/millibar>
        let Rd = 287.054&lt;joule/kilogram/kelvin>
        let gm = 9.784&lt;meter/second^2></span>
        let g = 9.80665&lt;meter/second^2>
        // Zero-altitude zenith dry delay (equation A-6)
        let z_hyd:float&lt;meter> = (1E-6 * k1 * Rd * P)
                                / gm
        // Zero-altitude zenith wet delay (equation A-7)
<span class="highlight-line">        let z_wet:float&lt;meter> = (1E-6 * k2 * Rd)
                               / (gm * (λ + 1.0) - β * Rd)
                               * e / T</span>
        // Height-mapped zenith dry delay (equation A-8)
        let d_hyd:float&lt;meter> = (1.0 - β * height / T)
                              ** (g / (Rd * β))
                               * z_hyd
        // Height-mapped zenith wet delay (equation A-9)
        let d_wet:float&lt;meter> = (1.0 - β * height / T)
                              ** ((λ + 1.0) * g / (Rd * β) - 1.0)
                               * z_wet
        // Combined zenith dry and wet delay (part of equation A-2)
        d_hyd + d_wet;
 
    // Calculate the tropospheric delay for a satellite given its elevation
    member this.GetCorrection(elevation:float&lt;degree>):float&lt;meter> =
        let Sin(angle:float&lt;degree>):float =
            Math.Sin(((float)angle * Math.PI / 180.0))
        // Pre-calculated zenith delay
        let d = ZenithDelay
        // Elevation mapping value (equation A-10)
        let m = 1.001 / sqrt (0.002001 + Sin(elevation) ** 2.0)
        // Tropospheric delay correction (equation A-2)
        -d * m;</code></pre>

            <p>
                To show how the unit system is used I have highlighted the relevant lines leading up to the <em>z_wet</em> equation. Because the code compiles I can be reasonably sure the equation is correct. If I had made a mistake and the units didn’t match up it would have shown as an error in exactly the same way as if the types didn’t match.
            </p>

            <p>
                This system isn’t perfect. You can still make mistakes that aren’t caught. But it gives an extra level of confidence that may well be worth it in some applications.
            </p>

            <pre><code class="csharp">TroposphereModel troposphere = new TroposphereModel(
    59.35264, 39.773, DateTime.Parse("2004-07-08"));
 
Debug.Assert(Math.Abs(troposphere.GetCorrection(60.4) + 2.791) &lt; 0.05);
Debug.Assert(Math.Abs(troposphere.GetCorrection(37.0) + 4.024) &lt; 0.05);
Debug.Assert(Math.Abs(troposphere.GetCorrection(12.3) + 11.144) &lt; 0.05);</code></pre>

            <p>
                Here is a piece of C# code where we call the F# code and check the results against a set of known values taken from a Nordnav-R30 GPS receiver.
            </p>

        </content>

        <footer>
            <a class="comment" itemprop="discussionUrl" href="https://github.com/Gieron/Blog/issues/new" target="_blank">Have an issue with the post?</a>
            <span class="author" itemprop="author">Henrik Ripa</span>
        </footer>
    </article>

    <footer></footer>
</body>
</html>